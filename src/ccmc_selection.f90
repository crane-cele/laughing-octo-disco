module ccmc_selection

! Module containing all ccmc selection subroutines.
! For full explanation see top of ccmc.F90.

use const, only: i0, int_p, int_64, p, dp, debug, depsilon

implicit none

contains

    subroutine select_cluster(rng, sys, psip_list, f0, ex_level, linked_ccmc, nattempts, normalisation, &
                              initiator_pop, D0_pos, cumulative_excip_pop, tot_excip_pop, min_size, max_size, &
                              logging_info, cdet, cluster)

        ! Select a random cluster of excitors from the excitors on the
        ! processor.  A cluster of excitors is itself an excitor.  For clarity
        ! (if not technical accuracy) in comments we shall distinguish between
        ! the cluster of excitors and a single excitor, from a set of which the
        ! cluster is formed.

        ! In:
        !    sys: system being studied
        !    psip_list: particle_t object containing current excip distribution on
        !       this processor.
        !    f0: bit string of the reference.
        !    ex_level: max number of excitations from the reference to include in
        !        the Hilbert space.
        !    nattempts: the number of times (on this processor) a random cluster
        !        of excitors is generated in the current timestep.
        !    linked_ccmc: if true then only sample linked clusters.
        !    normalisation: intermediate normalisation factor, N_0, where we use the
        !       wavefunction ansatz |\Psi_{CC}> = N_0 e^{T/N_0} | D_0 >.
        !    initiator_pop: the population above which a determinant is an initiator.
        !    D0_pos: position in the excip list of the reference.  Must be negative
        !       if the reference is not on the processor.
        !    cumulative_excip_population: running cumulative excip population on
        !        all excitors; i.e. cumulative_excip_population(i) = sum(particle_t%pops(1:i)).
        !    tot_excip_pop: total excip population.
        !    min_size: the minimum size cluster to allow.
        !    max_size: the maximum size cluster to allow.
        !    logging_info: derived type containing information on currently logging status

        ! NOTE: cumulative_excip_pop and tot_excip_pop ignore the population on the
        ! reference as excips on the reference cannot form a cluster and the rounds the
        ! population on all other excitors to the nearest integer (for convenience--see
        ! comments in do_ccmc).  Both these quantities should be generated by
        ! cumulative_population (or be in the same format).

        ! In/Out:
        !    rng: random number generator.
        !    cdet: information about the cluster of excitors applied to the
        !        reference determinant.  This is a bare det_info_t variable on input
        !        with only the relevant fields allocated.  On output the
        !        appropriate (system-specific) fields have been filled by
        !        decoding the bit string of the determinant formed from applying
        !        the cluster to the reference determinant.
        !    cluster:
        !        Additional information about the cluster of excitors.  On
        !        input this is a bare cluster_t variable with the excitors array
        !        allocated to the maximum number of excitors in a cluster.  On
        !        output all fields in cluster have been set.

        use determinants, only: det_info_t
        use ccmc_data, only: cluster_t
        use ccmc_utils, only: convert_excitor_to_determinant, collapse_cluster
        use excitations, only: get_excitation_level
        use dSFMT_interface, only: dSFMT_t, get_rand_close_open
        use qmc_data, only: particle_t
        use proc_pointers, only: decoder_ptr
        use utils, only: factorial
        use search, only: binary_search
        use sort, only: insert_sort
        use parallel, only: nprocs
        use system, only: sys_t
        use logging, only: write_logging_stoch_selection, logging_t

        type(sys_t), intent(in) :: sys
        type(particle_t), intent(in), target :: psip_list
        integer(i0), intent(in) :: f0(sys%basis%string_len)
        integer, intent(in) :: ex_level
        integer(int_64), intent(in) :: nattempts
        logical, intent(in) :: linked_ccmc
        integer, intent(in) :: D0_pos
        complex(p), intent(in) :: normalisation
        real(p), intent(in) :: initiator_pop
        real(p), intent(in) :: cumulative_excip_pop(:), tot_excip_pop
        integer :: min_size, max_size
        type(dSFMT_t), intent(inout) :: rng
        type(det_info_t), intent(inout) :: cdet
        type(cluster_t), intent(inout) :: cluster
        type(logging_t), intent(in) :: logging_info

        real(dp) :: rand
        real(p) :: psize
        complex(p) :: cluster_population, excitor_pop
        integer :: i, pos, prev_pos
        real(p) :: pop(max_size)
        logical :: hit, allowed, all_allowed

        ! We shall accumulate the factors which comprise cluster%pselect as we go.
        !   cluster%pselect = n_sel p_size p_clust
        ! where
        !   n_sel   is the number of cluster selections made;
        !   p_size  is the probability of choosing a cluster of that size;
        !   p_clust is the probability of choosing a specific cluster given
        !           the choice of size.

        ! Each processor does nattempts.
        ! However:
        ! * if min_size=0, then each processor is allowed to select the reference (on
        !   average) nattempts/2 times.  Hence in order to have selection probabilities
        !   consistent and independent of the number of processors being used (which
        !   amounts to a processor-dependent timestep scaling), we need to multiply the
        !   probability the reference is selected by nprocs.
        ! * assuming each excitor spends (on average) the same amount of time on each
        !   processor, the probability that X different excitors are on the same processor
        !   at a given timestep is 1/nprocs^{X-1).
        ! The easiest way to handle both of these is to multiply the number of attempts by
        ! the number of processors here and then deal with additional factors of 1/nprocs
        ! when creating composite clusters.
        ! NB within a processor those nattempts can be split amongst OpenMP
        ! threads though that doesn't affect this probability.
        cluster%pselect = real(nattempts*nprocs, p)

        ! Select the cluster size, i.e. the number of excitors in a cluster.
        ! For a given truncation level, only clusters containing at most
        ! ex_level+2 excitors.
        ! Following the process described by Thom in 'Initiator Stochastic
        ! Coupled Cluster Theory' (unpublished), each size, n_s, has probability
        ! p(n_s) = 1/2^(n_s+1), n_s=0,ex_level and p(ex_level+2)
        ! is such that \sum_{n_s=0}^{ex_level+2} p(n_s) = 1.

        ! This procedure is modified so that clusters of size min_size+n_s
        ! has probability 1/2^(n_s+1), and the max_size picks up the remaining
        ! probability from the series.
        rand = get_rand_close_open(rng)
        psize = 0.0_p
        cluster%nexcitors = -1
        do i = 0, max_size-min_size-1
            psize = psize + 1.0_p/2**(i+1)
            if (rand < psize) then
                ! Found size!
                cluster%nexcitors = i+min_size
                cluster%pselect = cluster%pselect/2**(i+1)
                exit
            end if
        end do
        ! If not set, then must be the largest possible cluster
        if (cluster%nexcitors == -1) then
            cluster%nexcitors = max_size
            cluster%pselect = cluster%pselect*(1.0_p - psize)
        end if

        ! If could be using logging set to easily identifiable nonsense value.
        if (debug) pop = -1_int_p

        ! Initiator approximation.
        ! This is sufficiently quick that we'll just do it in all cases, even
        ! when not using the initiator approximation.  This matches the approach
        ! used by Alex Thom in 'Initiator Stochastic Coupled Cluster Theory'
        ! (unpublished).
        ! Assume all excitors in the cluster are initiators (initiator_flag=0)
        ! until proven otherwise (initiator_flag=1).
        cdet%initiator_flag = 0

        ! Assume cluster is allowed unless collapse_cluster finds out otherwise
        ! when collapsing/combining excitors or if it could never have been
        ! valid
        allowed = min_size <= max_size
        ! For linked coupled cluster we keep building the cluster after a
        ! disallowed excitation so need to know if there has been a disallowed
        ! excitation at all
        all_allowed = allowed

        select case(cluster%nexcitors)
        case(0)
            call create_null_cluster(sys, f0, cluster%pselect, normalisation, initiator_pop, &
                                    cdet, cluster)
        case default
            ! Select cluster from the excitors on the current processor with
            ! probability for choosing an excitor proportional to the excip
            ! population on that excitor.  (For convenience, we use a probability
            ! proportional to the ceiling(pop), as it makes finding the right excitor
            ! much easier, especially for the non-composite algorithm, as well as
            ! selecting excitors with the correct (relative) probability.  The
            ! additional fractional weight is taken into account in the amplitude.)
            !
            ! Rather than selecting one excitor at a time and adding it to the
            ! cluster, select all excitors and then find their locations and
            ! apply them.  This allows us to sort by population first (as the
            ! number of excitors is small) and hence allows for a more efficient
            ! searching of the cumulative population list.

            do i = 1, cluster%nexcitors
                ! Select a position in the excitors list.
                pop(i) = get_rand_close_open(rng)*tot_excip_pop
            end do
            call insert_sort(pop(:cluster%nexcitors))
            prev_pos = 1
            do i = 1, cluster%nexcitors
                call binary_search(cumulative_excip_pop, pop(i), prev_pos, psip_list%nstates, hit, pos)
                ! Not allowed to select the reference as it is not an excitor.
                ! Because we treat (for the purposes of the cumulative
                ! population) the reference to have 0 excips, then
                ! cumulative_excip_pop(D0_pos) = cumulative_excip_pop(D0_pos-1).
                ! The binary search algorithm assumes each value in the array
                ! being searched is unique, which is not true, so we can
                ! accidentally find D0_pos.  As we want to find pos such that
                ! cumulative_excip_pop(pos-1) < pop <= cumulative_excip_pop(pos),
                ! then this means we actually need the slot before D0_pos.
                ! Correcting for this accident is much easier than producing an
                ! array explicitly without D0...
                ! If contain multiple spaces we can have this in a more general
                ! case, where an excitor has population in another space but not
                ! that which we're currently concerned with. More general test
                ! should account for this.
                do
                    if (pos == 1) exit
                    if (abs(cumulative_excip_pop(pos) - cumulative_excip_pop(pos-1)) > depsilon) exit
                    pos = pos - 1
                end do
                if (sys%read_in%comp) then
                    excitor_pop = cmplx(psip_list%pops(1,pos), psip_list%pops(2,pos),p)/psip_list%pop_real_factor
                else
                    excitor_pop = real(psip_list%pops(1,pos),p)/psip_list%pop_real_factor
                end if
                if (i == 1) then
                    ! First excitor 'seeds' the cluster:
                    cdet%f = psip_list%states(:,pos)
                    cdet%data => psip_list%dat(:,pos) ! Only use if cluster is non-composite!
                    cluster_population = excitor_pop
                    ! Counter the additional *nprocs above.
                    cluster%pselect = cluster%pselect/nprocs
                else
                    call collapse_cluster(sys%basis, f0, psip_list%states(:,pos), excitor_pop, cdet%f, &
                                          cluster_population, allowed)
                    if (.not.allowed) then
                        if (.not. linked_ccmc) exit
                        all_allowed = .false.
                    end if
                    ! Each excitor spends the same amount of time on each processor on
                    ! average.  If this excitor is different from the previous excitor,
                    ! then the probability this excitor is on the same processor as the
                    ! previous excitor is 1/nprocs.  (Note choosing the same excitor
                    ! multiple times is valid in linked CC.)
                    if (pos /= prev_pos) cluster%pselect = cluster%pselect/nprocs
                end if
                ! If the excitor's population is below the initiator threshold, we remove the
                ! initiator status for the cluster
                if (abs(excitor_pop) <= initiator_pop) cdet%initiator_flag = 3
                ! Probability of choosing this excitor = pop/tot_pop.
                cluster%pselect = (cluster%pselect*abs(excitor_pop))/tot_excip_pop
                cluster%excitors(i)%f => psip_list%states(:,pos)
                prev_pos = pos
            end do

            if (allowed) then
                cluster%excitation_level = get_excitation_level(f0, cdet%f)
                ! To contribute the cluster must be within a double excitation of
                ! the maximum excitation included in the CC wavefunction.
                allowed = cluster%excitation_level <= ex_level+2
            end if

            if (allowed.or.linked_ccmc) then
                ! We chose excitors with a probability proportional to their
                ! occupation.  However, because (for example) the cluster t_X t_Y
                ! and t_Y t_X collapse onto the same excitor (where X and Y each
                ! label an excitor), the probability of selecting a given cluster is
                ! proportional to the number of ways the cluster could have been
                ! formed.  (One can view this factorial contribution as the
                ! factorial prefactors in the series expansion of e^T---see Eq (8)
                ! in the module-level comments.)
                ! If two excitors in the cluster are the same, the factorial
                ! overcounts the number of ways the cluster could have been formed
                ! but the extra factor(s) of 2 are cancelled by a similar
                ! overcounting in the calculation of hmatel.
                cluster%pselect = cluster%pselect*factorial(cluster%nexcitors)

                ! Sign change due to difference between determinant
                ! representation and excitors and excitation level.
                call convert_excitor_to_determinant(cdet%f, cluster%excitation_level, cluster%cluster_to_det_sign, f0)
                call decoder_ptr(sys, cdet%f, cdet)

                ! Normalisation factor for cluster%amplitudes...
                cluster%amplitude = cluster_population/(normalisation**(cluster%nexcitors-1))
            else
                ! Simply set excitation level to a too high (fake) level to avoid
                ! this cluster being used.
                cluster%excitation_level = huge(0)
            end if

            if (.not.all_allowed) cluster%excitation_level = huge(0)

        end select

        if (debug) call write_logging_stoch_selection(logging_info, cluster%nexcitors, cluster%excitation_level, pop, &
                min(sys%nel, ex_level+2), cluster%pselect, cluster%amplitude, allowed)

    end subroutine select_cluster

    subroutine create_null_cluster(sys, f0, prob, D0_normalisation, initiator_pop, cdet, cluster)

        ! Create a cluster with no excitors in it, and set it to have
        ! probability of generation prob.

        ! In:
        !    sys: system being studied
        !    f0: bit string of the reference
        !    prob: The probability we set in it of having been generated
        !    D0_normalisation:  The number of excips at the reference (which
        !        will become the amplitude of this cluster)
        !    initiator_pop: the population above which a determinant is an initiator.

        ! In/Out:
        !    cdet: information about the cluster of excitors applied to the
        !        reference determinant.  This is a bare det_info variable on input
        !        with only the relevant fields allocated.  On output the
        !        appropriate (system-specific) fields have been filled by
        !        decoding the bit string of the determinant formed from applying
        !        the cluster to the reference determinant.
        !    cluster:
        !        Additional information about the cluster of excitors.  On
        !        input this is a bare cluster_t variable with the excitors array
        !        allocated to the maximum number of excitors in a cluster.  On
        !        output all fields in cluster have been set.

        use system, only: sys_t
        use determinants, only: det_info_t
        use ccmc_data, only: cluster_t
        use proc_pointers, only: decoder_ptr

        type(sys_t), intent(in) :: sys
        integer(i0), intent(in) :: f0(sys%basis%string_len)
        real(p), intent(in) :: prob, initiator_pop
        complex(p), intent(in) :: D0_normalisation
        type(det_info_t), intent(inout) :: cdet
        type(cluster_t), intent(inout) :: cluster

        ! Note only one null cluster to choose => p_clust = 1.
        cluster%pselect = prob

        cluster%nexcitors = 0

        ! Initiator approximation.
        ! This is sufficiently quick that we'll just do it in all cases, even
        ! when not using the initiator approximation.  This matches the approach
        ! used by Alex Thom in 'Initiator Stochastic Coupled Cluster Theory'
        ! (unpublished).
        ! Surely the reference has an initiator population?
        cdet%initiator_flag = 0

        ! Must be the reference.
        cdet%f = f0
        cluster%excitation_level = 0
        cluster%amplitude = D0_normalisation
        cluster%cluster_to_det_sign = 1
        ! If not initiator something has gone seriously wrong and the CC
        ! approximation is (most likely) not suitably for this system.
        ! Let the user be an idiot if they want to be...
        if (abs(D0_normalisation) <= initiator_pop) cdet%initiator_flag = 3

        call decoder_ptr(sys, cdet%f, cdet)

    end subroutine create_null_cluster

    subroutine select_nc_cluster(sys, psip_list, f0, iexcitor, initiator_pop, &
                                            cdet, cluster)

        ! Select (deterministically) the non-composite cluster containing only
        ! the single excitor iexcitor and set the same information as select_cluster.


        ! In:
        !    sys: system being studied
        !    psip_list: particle_t object containing current excip distribution on
        !       this processor.
        !    f0: bit string of the reference
        !    iexcitor: the index (in range [1,nstates]) of the excitor to select.
        !    initiator_pop: the population above which a determinant is an initiator.

        ! In/Out:
        !    cdet: information about the cluster of excitors applied to the
        !        reference determinant.  This is a bare det_info variable on input
        !        with only the relevant fields allocated.  On output the
        !        appropriate (system-specific) fields have been filled by
        !        decoding the bit string of the determinant formed from applying
        !        the cluster to the reference determinant.
        !    cluster:
        !        Additional information about the cluster of excitors.  On
        !        input this is a bare cluster_t variable with the excitors array
        !        allocated to the maximum number of excitors in a cluster.  On
        !        output all fields in cluster have been set.

        use system, only: sys_t
        use determinants, only: det_info_t
        use ccmc_data, only: cluster_t
        use ccmc_utils, only: convert_excitor_to_determinant
        use excitations, only: get_excitation_level
        use qmc_data, only: particle_t
        use search, only: binary_search
        use proc_pointers, only: decoder_ptr

        type(sys_t), intent(in) :: sys
        type(particle_t), intent(in), target :: psip_list
        integer(i0), intent(in) :: f0(sys%basis%string_len)
        integer(int_64), intent(in) :: iexcitor
        real(p), intent(in) :: initiator_pop
        type(det_info_t), intent(inout) :: cdet
        type(cluster_t), intent(inout) :: cluster
        complex(p) :: excitor_pop

        ! Rather than looping over individual excips we loop over different sites. This is
        ! because we want to stochastically set the number of spawning attempts such that
        ! we on average select each excitor a number of times proportional to the absolute
        ! population. As such, we can't specify the total number of selections beforehand
        ! within do_ccmc.

        ! As iterating deterministically through all noncomposite clusters, pselect = 1
        ! exactly.
        ! This excitor can only be selected on this processor and only one excitor is
        ! selected in the cluster, so unlike selecting the reference or composite
        ! clusters, there are no additional factors of nprocs or 1/nprocs to include.

        cluster%pselect = 1.0_p

        cluster%nexcitors = 1

        ! Initiator approximation.
        ! This is sufficiently quick that we'll just do it in all cases, even
        ! when not using the initiator approximation.  This matches the approach
        ! used by Alex Thom in 'Initiator Stochastic Coupled Cluster Theory'
        ! (unpublished).
        ! Assume all excitors in the cluster are initiators (initiator_flag=0)
        ! until proven otherwise (initiator_flag=1).
        cdet%initiator_flag = 0

        cdet%f = psip_list%states(:,iexcitor)
        cdet%data => psip_list%dat(:,iexcitor)
        cluster%excitors(1)%f => psip_list%states(:,iexcitor)
        if (sys%read_in%comp) then
            excitor_pop = cmplx(psip_list%pops(1,iexcitor), psip_list%pops(2,iexcitor),p)/psip_list%pop_real_factor
        else
            excitor_pop = real(psip_list%pops(1,iexcitor),p)/psip_list%pop_real_factor
        end if

        if (abs(excitor_pop) <= initiator_pop) cdet%initiator_flag = 3
        cluster%excitation_level = get_excitation_level(f0, cdet%f)
        cluster%amplitude = excitor_pop

        ! Sign change due to difference between determinant
        ! representation and excitors and excitation level.
        call convert_excitor_to_determinant(cdet%f, cluster%excitation_level, cluster%cluster_to_det_sign, f0)
        call decoder_ptr(sys, cdet%f, cdet)

    end subroutine select_nc_cluster

!---- Cluster information accumulation ---

    subroutine update_selection_data(selection_data, cluster)

        use ccmc_data, only: cluster_t, selection_data_t

        type(selection_data_t), intent(inout) :: selection_data
        type(cluster_t), intent(in) :: cluster

        associate( nex=>cluster%nexcitors, amp=>real(cluster%amplitude,kind=dp)/real(cluster%pselect,kind=dp), &
            aa=>selection_data%average_amplitude, &
            nsuccess=>selection_data%nsuccessful)

            if (cluster%excitation_level /= huge(0)) then
                aa(nex) = aa(nex) * (real(nsuccess(nex),kind=dp)/real(nsuccess(nex)+1_int_64,kind=dp)) &
                    + abs(amp)/ real(nsuccess(nex)+1_int_64, kind=dp)
                nsuccess(nex) = nsuccess(nex) + 1_int_64
            end if
        end associate

    end subroutine update_selection_data

!---- p_size Probability update functions ----

    subroutine set_psize_selections(selection_data, max_selections, min_size, max_size)
        use ccmc_data, only: selection_data_t
        type(selection_data_t), intent(inout) :: selection_data
        integer(int_64) :: nselections
        integer, intent(in) :: min_size, max_size
        integer(int_64), intent(in) :: max_selections

        if (max_size > 1) then

            ! Set total selections so that expected proportion of selections of noncomposite gives
            ! correct number of selections.
            nselections = ceiling(real(selection_data%nsingle_excitors, kind=dp) / &
                                    selection_data%size_weighting(1), kind=int_64)
            if (nselections <= selection_data%nD0_select) then
                ! Have majority of population on the reference; need to make sure we sample sensibly.
                ! Use conventional non-composite reference + noncomposite selection numbers.
                selection_data%nstochastic_clusters = ceiling(nselections * &
                    (1.0_dp - sum(selection_data%size_weighting(0:1))))
            else if (nselections > max_selections) then
                selection_data%nD0_select = ceiling((max_selections-selection_data%nsingle_excitors) * &
                            selection_data%size_weighting(0)/(1.0_dp - selection_data%size_weighting(1)))
                selection_data%nstochastic_clusters = ceiling((max_selections-selection_data%nsingle_excitors) * &
                            sum(selection_data%size_weighting(2:))/(1.0_dp - selection_data%size_weighting(1)))
            else
                ! Can treat reference more similarly to the rest of the space.
                selection_data%nD0_select = ceiling(nselections * selection_data%size_weighting(0))
                selection_data%nstochastic_clusters = ceiling(nselections * sum(selection_data%size_weighting(2:)))
            end if

            selection_data%nclusters = selection_data%nD0_select + selection_data%nsingle_excitors &
                                + selection_data%nstochastic_clusters
        end if
    end subroutine set_psize_selections

!---- p_comb Probability update functions ----

    subroutine update_selection_probabilities(cluster_selection, cumulative_excip_pop, nstates_excit_level)
        ! Updates all probabilities for selecting different excitation level combinations within
        ! cluster_selection object in accordance with cumulative population distribution and
        ! number of states per excitation level given.
        ! In:
        !    cumulative_excip_pop: cumulative excip population distribution.
        !    nstates_excit_level: number of states at each excitation level in (1,:),
        !       cumulative distribution in (2,:).
        ! In/Out:
        !    cluster_selection: selection_data_t object containing all information required for
        !       truncated selection (all valid excitation level combinations). On output
        !       cluster_sizes_proportion will be updated.

        use ccmc_data, only: selection_data_t
        type(selection_data_t), intent(inout) :: cluster_selection
        integer(int_p), intent(in), allocatable :: cumulative_excip_pop(:)
        integer, intent(in), allocatable :: nstates_excit_level(:,:)
        integer :: i
        integer(int_64), allocatable :: excitation_level_population(:)
        real(dp), allocatable :: real_excitation_level_population(:)

        allocate(excitation_level_population(lbound(nstates_excit_level, dim=2):ubound(nstates_excit_level, dim=2)))
        allocate(real_excitation_level_population(lbound(nstates_excit_level, dim=2):ubound(nstates_excit_level, dim=2)))

        excitation_level_population(:) = 0_int_64

        do i = lbound(excitation_level_population, dim=1)+1, ubound(excitation_level_population,dim=1)
            if (nstates_excit_level(2,i) > 0) then
                excitation_level_population(i) = cumulative_excip_pop(nstates_excit_level(2,i))
                if (nstates_excit_level(2,i-1) > 0)  then
                    excitation_level_population(i) = excitation_level_population(i) - &
                        cumulative_excip_pop(nstates_excit_level(2,i-1))
                end if
            end if
        end do

        real_excitation_level_population = real(excitation_level_population, dp)

        do i = lbound(cluster_selection%cluster_sizes_info, dim=1), ubound(cluster_selection%cluster_sizes_info, dim=1)
            associate(select_info => cluster_selection%cluster_sizes_info(i)%v, &
                select_proportion => cluster_selection%cluster_sizes_proportion(i)%v)
                call update_selection_block_probability(select_proportion, select_info, &
                        real_excitation_level_population, cluster_selection%size_weighting(i))
            end associate
        end do

        deallocate(excitation_level_population)
        deallocate(real_excitation_level_population)

    end subroutine update_selection_probabilitie


!---- Initialisation routines for improved selection information ----

    subroutine init_selection_data(ex_level, selection_data)
        ! Take cluster selection object and initialise all data required for calculation.
        ! In:
        !   ex_level: maximum excitation level allowed in calculation.
        ! In/Out:
        !   cluster_selection: selection_data_t object. On output cluster_sizes_info components
        !       will be allocated and set as appropriate, and cluster_sizes_proportion allocated.
        use ccmc_data, only: selection_data_t
        use checking, only: check_allocate
        use parallel, only: parent

        integer, intent(in) :: ex_level
        type(selection_data_t), intent(inout) :: selection_data

        call init_possible_clusters(ex_level, selection_data)

        call init_psize_data(ex_level, selection_data)

    end subroutine init_selection_data

    subroutine init_possible_clusters(ex_level, selection_data)
        ! Take cluster selection object and initialise all possible size combinations of clusters.
        ! In:
        !   ex_level: maximum excitation level allowed in calculation.
        ! In/Out:
        !   cluster_selection: selection_data_t object. On output cluster_sizes_info components
        !       will be allocated and set as appropriate, and cluster_sizes_proportion allocated.
        use ccmc_data, only: selection_data_t
        use checking, only: check_allocate
        use parallel, only: parent
        integer, intent(in) :: ex_level
        type(selection_data_t), intent(inout) :: selection_data

        integer :: nclusters, cluster_size, ierr, dummy
        integer :: temporary(1:ex_level), i

        allocate(selection_data%cluster_sizes_info(2:ex_level+2), stat=ierr)
        call check_allocate('cluster_selection%cluster_sizes_info',ex_level+2, ierr)
        allocate(selection_data%cluster_sizes_proportion(2:ex_level+2), stat=ierr)
        call check_allocate('cluster_selection%cluster_sizes_proportion',ex_level+2, ierr)

        ! Go through possible sizes...
        do cluster_size = 2, ex_level + 2
            ! Calc number possible clusters for each
            nclusters = calc_available_perms(cluster_size, 1, 0, ex_level)
            ! Allocate storage of appropriate size to store all cluster info
            allocate(selection_data%cluster_sizes_info(cluster_size)%v(1:nclusters,1:ex_level), stat=ierr)
            call check_allocate('cluster_selection_component', nclusters*ex_level,ierr)

            allocate(selection_data%cluster_sizes_proportion(cluster_size)%v(1:nclusters), stat=ierr)
            call check_allocate('cluster_selection_component', nclusters,ierr)

            selection_data%cluster_sizes_info(cluster_size)%v(:,:) = 0
            selection_data%cluster_sizes_proportion(cluster_size)%v(:) = 0
            dummy = 0
            temporary(:) = 0
            call find_available_perms(dummy, selection_data%cluster_sizes_info(cluster_size)%v, temporary, &
                                    cluster_size, 1, 0, ex_level)
            if (parent) then
                write(6, *), 'Found', nclusters,'clusters of size', cluster_size,'.'
                write(6, *), 'Clusters are:'
                do i = 1, nclusters
                    write(6, *), selection_data%cluster_sizes_info(cluster_size)%v(i, :)
                end do
            end if
        end do

    end subroutine init_possible_clusters

    subroutine init_psize_data(ex_level, selection_data)
        ! Take cluster selection object and initialise all data required for psize variation.
        ! In:
        !   ex_level: maximum excitation level allowed in calculation.
        !   cluster_selection: selection_data_t object. On output cluster_sizes_info components
        !       will be allocated and set as appropriate, and cluster_sizes_proportion allocated.
        use ccmc_data, only: selection_data_t
        use checking, only: check_allocate

        integer, intent(in) :: ex_level
        type(selection_data_t), intent(inout) :: selection_data
        integer :: ierr, i

        allocate(selection_data%size_weighting(0:ex_level+2), stat=ierr)
        call check_allocate('size_weighting', ex_level + 3, ierr)
        allocate(selection_data%cumulative_size_weighting(0:ex_level+2), stat=ierr)
        call check_allocate('cumulative_size_weighting', ex_level + 3, ierr)

        associate(size_weighting => selection_data%size_weighting, &
                cumulative_size_weighting => selection_data%cumulative_size_weighting)

            size_weighting(0) = 0.5_dp
            cumulative_size_weighting(0) = 0.5_dp

            do i = 1, ex_level+1
                size_weighting(i) = 1.0_dp/(2.0_dp**(i+1))
                cumulative_size_weighting(i) = cumulative_size_weighting(i-1) + size_weighting(i)
            end do

            size_weighting(ex_level+2) = 1.0_dp - cumulative_size_weighting(ex_level+1)
            cumulative_size_weighting(ex_level+2) = 1.0_dp

        end associate

        allocate(selection_data%average_amplitude(0:ex_level+2), stat=ierr)
        call check_allocate('average_amplitude', ex_level + 3, ierr)
        allocate(selection_data%nsuccessful(0:ex_level+2), stat=ierr)
        call check_allocate('nsuccessful', ex_level + 3, ierr)

        selection_data%average_amplitude = 0.0_dp
        selection_data%nsuccessful = 0_int_64

    end subroutine init_psize_data

!---- Cluster generation functions ----
! Below functions generate all allowed excitation level combinations for clusters satifying given
! initial conditions.
! This is achieved by starting from lowest excitation level of excitor considered, iterating through all
! permissible numbers of said excitors, and depending upon the properties of any suggested combination
! rejecting it, accepting it or calling the same function for the next excitation level to search for
! acceptable combinations.

     pure recursive function calc_available_perms(selections_remain, excitor_excit_level, current_excit_level, &
                                 max_excit_level) result(nfound)
         ! For given number of selections and excitations remaining,
         ! calculate number of possible clusters. Performs this via a recursive
         ! search of all combinations.
         ! In:
         !   selections_remain: total number more excitors to be added to the
         !       cluster to reach chosen total size.
         !   excitor_excit_level: excitation level of excitors currently being
         !       considered for addition to the cluster.
         !   current_excit_level: excitation level of partial cluster as it stands.
         !   max_excit_level: maximum excitation level permitted in calculation.
         ! Returns:
         !   Number of possible size combinations fitting given initial conditions.
         integer, intent(in) :: selections_remain, excitor_excit_level, current_excit_level
         integer, intent(in) :: max_excit_level
         integer :: nfound
         integer :: new_selections_remain, new_current_excit_level
         integer :: i

         nfound = 0

         if (max_excit_level + 2 - current_excit_level >= excitor_excit_level * selections_remain) then
             ! Is still possible to form a valid cluster.
             do i = 0, selections_remain
                 new_selections_remain = selections_remain - i
                 new_current_excit_level = current_excit_level + i * (excitor_excit_level)
                 if (new_selections_remain == 0) then
                     ! Have selected appropriate number of excitors to give cluster
                     ! of required size without exceeding excitation level; increase.
                     nfound = nfound + 1
                 else if (excitor_excit_level < max_excit_level) then
                     ! Haven't exceeded allowed excitation level, so have to
                     ! continue searching at higher levels.
                     nfound = nfound + calc_available_perms(new_selections_remain, excitor_excit_level + 1, &
                                 new_current_excit_level, max_excit_level)
                 end if
             end do
         end if

     end function calc_available_perms

     pure recursive subroutine find_available_perms(nfound, main_storage, temporary, selections_remain, &
                                    excitor_excit_level, current_excit_level, max_excit_level)
        ! For given number of selections and max excitation level calculate
        ! all possible excitation level combinations forming a valid
        ! cluster recursively. Once sufficient excitors are selected the overall
        ! combination is stored in 'main_storage' in the next free slot.

        ! main_storage should be allocated to the correct size before
        ! initialisation; calc_available_perms enables calculations of the
        ! size required.
        ! In:
        !   temporary: 1d integer array containing information on excitation
        !       level combination accrued so far in recursive search.
        !   selections_remain: total number more excitors to be added to the
        !       cluster to reach chosen total size.
        !   excitor_excit_level: excitation level of excitors currently being
        !       considered for addition to the cluster.
        !   current_excit_level: excitation level of cluster as currently
        !       stands.
        !   max_excit_level: maximum excitation level permitted in calculation.
        ! In/Out:
        !   nfound: number of allowed combinations already found.
        !   main_storage: 2d integer array to store all found combinations.
        !       main_storage(j,k) stores number of individual excitors of
        !       excitation level k in combination j.
        integer, allocatable, intent(inout) :: main_storage(:,:)
        integer, intent(in) :: temporary(:), max_excit_level, excitor_excit_level
        integer, intent(inout) :: nfound
        integer, intent(in) :: selections_remain, current_excit_level
        integer :: new_selections_remain, new_current_excit_level
        integer :: i, nmax, ierr
        integer :: temporary_loc(1:max_excit_level)

        temporary_loc = temporary

        if (max_excit_level + 2 - current_excit_level >= excitor_excit_level * selections_remain) then
            ! Is still possible to form a valid cluster.
            do i = 0, selections_remain
                new_selections_remain = selections_remain - i
                new_current_excit_level = current_excit_level + i * excitor_excit_level
                if (new_selections_remain == 0) then
                    temporary_loc(excitor_excit_level) = i
                    nfound = nfound + 1
                    main_storage(nfound, :) = temporary_loc(:)
                else if (excitor_excit_level < max_excit_level) then
                    temporary_loc(excitor_excit_level) = i
                    call find_available_perms(nfound, main_storage, temporary_loc, new_selections_remain, &
                            excitor_excit_level + 1, new_current_excit_level, max_excit_level)
                end if
            end do
        end if

    end subroutine find_available_perms

!---- Helper Functions -----

    pure function calc_combination_weighting(ar1, v1) result(res)

        ! Calculates overall weighting of combination of excitation levels
        ! ar1 given population distribution by population level of v1.

        ! Combines two vectors together such that
        !   res = product_{i, ar1(i)=/0} ar1_{i} ** v1{i} / v1!

        use utils, only: factorial
        integer, intent(in) :: ar1(:)
        real(dp), intent(in) :: v1(:)
        real(dp) :: res
        real(dp) :: v2(size(v1,dim=1))
        integer :: i
        logical :: mask(size(ar1,dim=1))

        mask = .true.

        do i = lbound(ar1, dim=1), ubound(ar1, dim=1)
           v2(i) = real(v1(i),kind=dp) ** ar1(i) / factorial(ar1(i))
           if (ar1(i) == 0.0_dp) mask(i) = .false.
        end do

        res = real(product(v2, dim=1, mask=mask), dp)

    end function calc_combination_weighting

    subroutine update_cumulative_size_weighting(size_weighting, cumulative_size_weighting)
        real(dp), intent(inout), allocatable :: size_weighting(:)
        real(dp), intent(inout), allocatable :: cumulative_size_weighting(:)
        integer :: i

        cumulative_size_weighting(lbound(cumulative_size_weighting,dim=1)) = size_weighting(lbound(size_weighting,dim=1))
        do i = lbound(size_weighting, dim=1) + 1, ubound(size_weighting, dim=1) - 1
            cumulative_size_weighting(i) = cumulative_size_weighting(i-1) + size_weighting(i)
        end do

        cumulative_size_weighting(ubound(size_weighting, dim=1)) = 1.0_dp

        if (size(cumulative_size_weighting, dim=1) > 1) then
            size_weighting(ubound(size_weighting, dim=1)) = &
                    cumulative_size_weighting(ubound(size_weighting, dim=1)) - &
                    cumulative_size_weighting(ubound(size_weighting, dim=1) - 1)
        end if
    end subroutine update_cumulative_size_weighting

end module ccmc_selection
